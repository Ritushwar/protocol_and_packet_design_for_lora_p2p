#include "protocol_state.h"

uint8_t my_user_id;
uint8_t my_node_id;
uint8_t user_id_r;
uint8_t node_id_r;
uint8_t msg_id;
uint8_t msg_id_r;
std::string msg   = "";
uint8_t input_buffer[64] = {0};       // sender
uint8_t encrypted_buffer[64] = {0};   // sender
size_t enc_len = 0;
uint16_t crc;

uint8_t received_payload[64] = {0};   // receiver
size_t payload_len;                   // receiver
uint16_t received_crc;                // receiver
uint8_t decrypted_payload[64] = {0};

#define TIMEOUT_MS 3000UL
#define POST_SUCCESS_DELAY 100UL
static unsigned long rts_start_ms = 0;
static unsigned long data_start_ms = 0;
static unsigned long cts_start_ms = 0;
static unsigned long success_start_ms = 0;
int max_retry = 3;
int rts_retry_c = 0;
int data_retry_c = 0;

enum ProtoState {
    IDLE,
    SEND_RTS,
    WAIT_CTS,
    SEND_DATA,
    WAIT_ACK,
    SUCCESS,
    SEND_CTS,
    WAIT_DATA,  
    SEND_ACK,
    RETRY,
    ERROR
};

static ProtoState protoState = IDLE;

static bool cts_received  = false;
static bool ack_received  = false;
static bool rts_received  = false;
static bool data_received = false;
static bool retry_received = false;

static bool channel_busy = false;

void reset_channel() {
    cts_received  = false;
    ack_received  = false;
    rts_received  = false;
    data_received = false;
    channel_busy  = false;
    rts_retry_c   = 0;
    data_retry_c  = 0;
}

bool is_for_me(uint8_t u_id_r, uint8_t n_id_r){
    if(u_id_r== my_user_id && n_id_r == my_node_id){
        return true;
    }
    else{
        return false;
    }
}
void set_my_add(uint8_t user_id, uint8_t node_id) {
    my_user_id = user_id;
    my_node_id = node_id;
}

void set_r_add(uint8_t user_id, uint8_t node_id) {
    user_id_r = user_id;
    node_id_r = node_id;
}

void send_rts() {
    Serial.println("Sending RTS");
    lora_packet_struct rts_pkt = get_rts(my_user_id, my_node_id, user_id_r, node_id_r, msg_id);
    send_packet(rts_pkt);
}

void send_cts() {
    Serial.println("Sending CTS");
    lora_packet_struct cts_pkt = get_cts(my_user_id, my_node_id, user_id_r, node_id_r, msg_id_r);
    send_packet(cts_pkt);
}

void send_data() {
    Serial.println("Sending DATA");
    lora_packet_struct data_pkt = get_data_pkt(my_user_id, my_node_id, user_id_r, node_id_r, msg_id, encrypted_buffer, enc_len, crc);
    send_packet(data_pkt);
}

void send_ack() {
    Serial.println("Sending ACK");
    lora_packet_struct ack_pkt = get_ack(my_user_id, my_node_id, user_id_r, node_id_r, msg_id_r);
    send_packet(ack_pkt);
}

void serial_update(){
    if(serialIO_available() && !channel_busy && protoState == IDLE){
        msg = serialIO_read();

        // copy plaintext
        memcpy(input_buffer, msg.c_str(), msg.length());

        enc_len = encrypt_msg(input_buffer, msg.length(), encrypted_buffer);

        Serial.print("Encrypted message: ");
        for (size_t i = 0; i < enc_len; i++) {
            if (encrypted_buffer[i] < 0x10) Serial.print("0"); // leading zero
            Serial.print(encrypted_buffer[i], HEX);
            Serial.print(" ");
        }
        Serial.println();

        crc = calc_crc_bytes(encrypted_buffer, enc_len);

        serialIO_print("My msg crc: " + std::to_string(crc));
        channel_busy = true;
        protoState = SEND_RTS;
    }

    if(payload_len > 0){
        uint16_t calculated_crc = calc_crc_bytes(received_payload, payload_len);
        serialIO_print("Received CRC: " + std::to_string(received_crc));
        serialIO_print("Calculated CRC: " + std::to_string(calculated_crc));
        if(calculated_crc != received_crc){
            serialIO_print("Error in the received message: ");
            protoState = ERROR;
            payload_len = 0;
            received_crc = 0x0000;    // reset crc
            return;
        }
        serialIO_print("Received before decryption: ");
        for (size_t i = 0; i < payload_len; i++) {
            if (received_payload[i] < 0x10) Serial.print("0"); // leading zero
            Serial.print(received_payload[i], HEX);
            Serial.print(" ");
        }
        Serial.println();
        decrypt_msg(received_payload, payload_len, decrypted_payload);
        Serial.println((char*)decrypted_payload);
        payload_len = 0;
    }
    return;
}

void lora_update(){
    if(!is_packet_available())  return;

    size_t pkt_size = get_packet_size();
    if(pkt_size < 8){
        Serial.print("Invalid packet size of: ");
        Serial.println(pkt_size);
        lora_packet_struct random_pkt = get_packet(pkt_size);    // discard the packet
        return;
    }
    else{
       Serial.print("Valid packet size of: "); 
       Serial.println(pkt_size);
    }

    lora_packet_struct pkt = get_packet(pkt_size);
    
    // check if the packet is addressed to me
    if(!is_for_me(pkt.USER_ID_R, pkt.NODE_ID_R)){
        Serial.print("Packet is not for me "); 
        return;
    }

    switch (pkt.PKT_TYPE)
    {
    case 0x01:   // RTS
        if(!channel_busy){
            msg_id_r = pkt.MSG_ID;
            rts_received = true;
        }else{
            Serial.println("RTS ignored — channel busy");
        }
        break;
    
    case 0x02:    // CTS
        cts_received = true;
        break;
    
    case 0x03:    // DATA
        msg_id_r = pkt.MSG_ID;
        payload_len = pkt.PAY_LEN;
        memcpy(received_payload, pkt.PAYLOAD, payload_len);
        received_crc  = (uint16_t)pkt.PAYLOAD[payload_len] << 8;
        received_crc |= pkt.PAYLOAD[payload_len + 1];
        serialIO_print("My received crc: " + std::to_string(received_crc));
        data_received = true;
        break;
    
    case 0x04:  // ACK
        if(pkt.MSG_ID == msg_id){
            ack_received = true;
        }
        break;

    case 0x06:  // retry
        if(pkt.MSG_ID == msg_id){
            retry_received = true;
        }
    default:
        break;
    }
}

void fsm_update(){
    switch (protoState)
    {
    case IDLE:
        // small cooldown after sucess before accepting new RTS and success
        if (millis() - success_start_ms < POST_SUCCESS_DELAY) break;
        if(rts_received){
            rts_received = false;
            protoState = SEND_CTS;
        }
        break;
    
    case SEND_RTS:
        send_rts();
        rts_start_ms = millis();
        protoState = WAIT_CTS;
        break;
    
    case WAIT_CTS:
        if (cts_received){
            cts_received = false;
            protoState = SEND_DATA;
        }
        else if (millis()- rts_start_ms > TIMEOUT_MS)
        {
            Serial.println("Timeout waiting for CTS, retrying....");
            rts_retry_c++;
            if(rts_retry_c <= max_retry){
                protoState = SEND_RTS;
            }else{
                reset_channel();
                protoState = IDLE;
            }

        }
        break;

    case SEND_DATA:
        send_data();
        data_start_ms = millis();
        protoState = WAIT_ACK;
        break;
    
    case WAIT_ACK:
        if(retry_received){
            protoState = RETRY;
        }
        if(ack_received){
            ack_received = false;
            protoState = SUCCESS;
        }
        else if(millis() - data_start_ms > TIMEOUT_MS){
            Serial.println("Timeout waiting for ack, retrying ......");
            data_retry_c++;
            if(data_retry_c <= max_retry){
                protoState = SEND_DATA;
            }else{
                reset_channel();
                protoState = IDLE;
            }
        }
        break;
    
    case SUCCESS:
        Serial.println("Message sent successfully!");
        received_crc = 0x0000;     // reset the crc
        msg_id++;
        success_start_ms = millis();
        reset_channel();
        protoState = IDLE;
        break;
    
    case SEND_CTS:
        send_cts();
        cts_start_ms = millis();
        protoState = WAIT_DATA;
        break;
    
    case WAIT_DATA:
        if(data_received){
            data_received = false;
            protoState = SEND_ACK;
        }
        else if(millis() - cts_start_ms > TIMEOUT_MS){
            Serial.println("Timeout waiting for DATA, return to IDLE");
            reset_channel();
            protoState = IDLE;
        }
        break;
    
    case SEND_ACK:
        send_ack();
        reset_channel();
        protoState = IDLE;
        break;

    case RETRY:
        if(retry_received){
            retry_received = false;
            protoState = SEND_DATA;
        }
        break;
    
    case ERROR:
        protoState = WAIT_DATA;
    default:
        break;
    }
}


